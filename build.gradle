import net.nemerosa.ontrack.gradle.ComposeStop
import net.nemerosa.ontrack.gradle.ComposeUp
import net.nemerosa.ontrack.gradle.DockerStart
import net.nemerosa.ontrack.gradle.DockerStop

buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
        kotlinVersion = '1.1.4-3'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-aggregate-javadocs-plugin:3.0.1'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
    }
}

plugins {
    id 'net.nemerosa.versioning' version '2.6.0'
    id 'nebula.os-package' version '2.2.6'
    id 'org.sonarqube' version '2.3'
}

/**
 * Profiles
 */

ext {
    documentationProfile = project.hasProperty('documentation')
}

/**
 * Meta information
 */

allprojects {
    group = 'net.nemerosa.ontrack'
    version = versioning.info.display
}

/**
 * Resolution
 */

allprojects {
    repositories {
        mavenCentral()
        jcenter()
    }
}


/**
 * Integration test environment
 */

ext {
    itJdbcUrl = project.properties.itJdbcUrl
    itJdbcUsername = project.properties.itJdbcUsername
    itJdbcPassword = project.properties.itJdbcPassword
    itJdbcWait = project.properties.itJdbcWait
}

// Pre-integration tests: starting Postgresql

task preIntegrationTest(type: ComposeUp) {
    machine = project.properties.itMachine
    dir = file("${rootDir}/gradle/it")
    projectFiles = ['docker-compose.yml']
    projectName = project.properties.itProject
    forceRecreate = false
    host = project.properties.itJdbcHost
    // When done
    doLast {
        def jdbcUrl = "jdbc:postgresql://${host}:${getPublishedPort('db', 5432)}/ontrack"
        project.ext.itJdbcUrl = jdbcUrl
        logger.info("JDBC URL = ${project.ext.itJdbcUrl}")
        logger.info("JDBC. Wait ${itJdbcWait} seconds for DB container to be ready...")
        sleep ((itJdbcWait as int) * 1000)
    }
}

// Post-integration tests: stopping Postgresql

task postIntegrationTest(type: ComposeStop) {
    machine = project.properties.itMachine
    dir = file("${rootDir}/gradle/it")
    projectFiles = ['docker-compose.yml']
    projectName = project.properties.itProject
    remove = true
}

/**
 * Java projects
 */

ext {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

def javaProjects = subprojects.findAll {
    it.path != ':ontrack-web'
}

configure(javaProjects) {

    /**
     * For all Java projects
     */

    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'kotlin-spring'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
        dependencies {
            dependency 'commons-io:commons-io:2.5'
            dependency 'org.apache.commons:commons-lang3:3.4'
            dependency 'org.projectlombok:lombok:1.16.10'
            dependency 'net.jodah:failsafe:0.9.2'
            dependency 'commons-logging:commons-logging:1.2'
            dependency 'org.apache.commons:commons-math3:3.6.1'
            dependency 'com.google.guava:guava:19.0'
            dependency 'net.sf.dbinit:dbinit:1.4.0'
            dependency 'args4j:args4j:2.33'
            dependency 'org.jgrapht:jgrapht-core:0.9.2'
            dependency 'org.kohsuke:groovy-sandbox:1.10'
            dependency 'org.postgresql:postgresql:9.4.1208'
            dependency 'org.flywaydb:flyway-core:4.0'
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}"
        compile "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        compileOnly 'org.projectlombok:lombok'
        testCompile 'org.projectlombok:lombok'
        // Testing
        testCompile 'junit:junit'
        testCompile 'org.mockito:mockito-core'
        testCompile "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    // Unit tests run with the `test` task
    test {
        include '**/*Test.class'
        reports {
            html.enabled = false
        }
    }

    // Integration tests
    task integrationTest(type: Test, dependsOn: 'test') {
        include '**/*IT.class'
        jvmArgs '-Xmx512m'
        reports {
            html.enabled = false
        }
        dependsOn preIntegrationTest
        finalizedBy postIntegrationTest
        /**
         * Sets the JDBC URL
         */
        doFirst {
            println "Setting JDBC URL for IT: ${rootProject.ext.itJdbcUrl}"
            systemProperty 'it.jdbc.url', rootProject.ext.itJdbcUrl
            systemProperty 'it.jdbc.user', rootProject.ext.itJdbcUsername
            systemProperty 'it.jdbc.password', rootProject.ext.itJdbcPassword
        }
    }

    // Acceptance tests
    task acceptanceTest(type: Test, dependsOn: 'integrationTest') {
        include '**/ACC*.class'
        ignoreFailures true
        reports {
            html.enabled = false
        }
    }

    // Javadoc

    if (documentationProfile) {
        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        // Sources

        task sourceJar(type: Jar) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
    }

    // POM file

    publishing {
        publications {
            mavenCustom(MavenPublication) {
                from components.java
                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', project.name)
                    root.appendNode('description', project.description ?: project.name)
                    root.appendNode('url', 'http://nemerosa.github.io/ontrack')

                    def licenses = root.appendNode('licenses')
                    def license = licenses.appendNode('license')
                    license.appendNode('name', 'The MIT License (MIT)')
                    license.appendNode('url', 'http://opensource.org/licenses/MIT')
                    license.appendNode('distribution', 'repo')

                    def scm = root.appendNode('scm')
                    scm.appendNode('url', 'https://github.com/nemerosa/ontrack')
                    scm.appendNode('connection', 'scm:git://github.com/nemerosa/ontrack')
                    scm.appendNode('developerConnection', 'scm:git://github.com/nemerosa/ontrack')

                    def developers = root.appendNode('developers')
                    def developer = developers.appendNode('developer')
                    developer.appendNode('id', 'dcoraboeuf')
                    developer.appendNode('name', 'Damien Coraboeuf')
                    developer.appendNode('email', 'damien.coraboeuf@gmail.com')
                }
            }
        }
    }

    model {
        tasks.generatePomFileForMavenCustomPublication {
            destination = file("${buildDir}/poms/${project.name}-${version}.pom")
        }
    }

    afterEvaluate {
        tasks.assemble.dependsOn 'generatePomFileForMavenCustomPublication'
    }

    // Archives for Javadoc and Sources

    artifacts {
        if (documentationProfile) {
            archives javadocJar
            archives sourceJar
        }
    }

}

/**
 * Packaging for delivery
 */

apply from: 'gradle/packaging.gradle'

/**
 * Packaging for OS
 */

apply from: 'gradle/os-packaging.gradle'

/**
 * Docker tasks
 */

apply from: 'gradle/docker.gradle'

/**
 * Acceptance tasks
 */

apply from: 'gradle/acceptance.gradle'

/**
 * Development tasks
 */

apply from: 'gradle/development.gradle'

/**
 * Acceptance tasks for OS packaging
 */

apply from: 'gradle/acceptance-os-package.gradle'

/**
 * Publication tasks
 *
 * Standalone Gradle tasks in `gradle/publication.gradle` and in
 * `gradle/production.gradle`
 */

/**
 * General test report
 */

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` tasks in all Java subprojects
    reportOn javaProjects*.test, javaProjects*.integrationTest, javaProjects*.acceptanceTest
}

/**
 * Wrapper definition
 */

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
}
