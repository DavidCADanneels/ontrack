/**
 * Packaging of Gradle related files
 */

apply plugin: 'base'

// ZIP package which contains all build scripts

task deliveryPackage(type: Zip) {
    from projectDir
    include 'buildSrc/**'
    include '*.gradle'
    include 'gradlew*'
    include 'gradle/**'
    include 'gradle.properties'
    exclude '**/.gradle/**'
    exclude '**/build/**'
    classifier = 'delivery'
}

build.dependsOn deliveryPackage

// ZIP package which contains all artifacts to be published

subprojects {
    afterEvaluate {
        // Only for Java projects
        if (tasks.findByName('jar')) {
            task publicationPackage(type: Zip) {
                dependsOn assemble
                classifier = 'publication'
                from jar
                if (tasks.findByName('javadocJar')) from javadocJar
                if (tasks.findByName('sourceJar')) from sourceJar
                if (tasks.findByName('writeNewPom')) from writeNewPom.output
            }
        }
    }
}
