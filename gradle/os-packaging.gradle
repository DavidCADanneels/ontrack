/**
 * Packaging of Ontrack for Linux OS (RPM + Debian)
 */

/**
 * The package version does not accept versions like the ones generated
 * from the Versioning plugin for the feature branches for example.
 */
def packageVersion = versioning.info.branchType != 'release' ? "0." + versioning.info.display : versioning.info.display

ospackage {
    packageName = 'ontrack'
    version = packageVersion
    release = '1'
    os = LINUX // only applied to RPM

    into '/opt/ontrack'

    from(project(':ontrack-ui').files("build/libs/ontrack-ui-${versioning.info.full}.jar")) {
        into 'lib'
    }

    from(files("gradle/os-package")) {
        include 'ontrack'
        into 'bin'
        fileMode = 0550
    }
}

task debPackage(type: Deb, dependsOn: ':ontrack-ui:bootRepackage') {
    link '/etc/init.d/ontrack', '/opt/ontrack/bin/ontrack'
}

task osPackages {
    // dependsOn rpmPackage
    dependsOn debPackage
}
