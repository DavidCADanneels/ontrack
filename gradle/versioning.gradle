/**
 * Versioning policy.
 *
 * The following information must be collected:
 *      - the full version - will be used for the project's version.
 *      - the base version - version information without the build number
 *      - the build version - the build number
 *      - the commit - the commit for this version
 *      - the source - where the version was computed from
 *
 * The way this information is collected depends on the environment:
 *
 * - if not .git information is available, we cannot determine the version:
 *     - base = LOCAL
 *     - build = <none>
 *     - full = LOCAL
 *     - commit = <none>
 *     - source = <none>
 *
 * - from a tag (we want to build from a tag)
 *     - the working copy is associated with a tag
 *     - base = TAG
 *     - build = <abbrev. sha1>
 *     - full = TAG
 *     - commit = <sha1>
 *     - source = 'tag'
 *
 * - from a branch <b>:
 *     - base = <b>
 *     - build = <abbrev. sha1>
 *     - full = <base>-<build>
 *     - commit = <sha1>
 *     - source = <b>
 */

// Information to collect
def versionBase
def versionBuild
def versionFull
def versionCommit
def versionSource

boolean hasGit = project.file('.git').exists()

def normalise(String value) {
    value.replaceAll(/[^A-Za-z0-9\.\-_]/, '-')
}

// No git information
if (!hasGit) {
    versionBase = 'LOCAL'
    versionBuild = ''
    versionFull = 'LOCAL'
    versionCommit = ''
    versionSource = ''
}

// We can use git
else {
    // Gets the current commit (short hash)
    versionBuild = 'git log -1 --format=%h'.execute().text.trim()
    // Gets the current commit (full hash)
    versionCommit = 'git log -1 --format=%H'.execute().text.trim()
    // Gets the tag info
    def tagInfo = 'git describe --exact-match --tags'.execute().text.trim()
    // We have a tag info
    if (tagInfo) {
        versionBase = normalise(tagInfo)
        versionFull = versionBase
        versionSource = 'tag'
    }
    // We do not have tag info
    else {
        // Gets the current branch
        versionBase = normalise('git rev-parse --abbrev-ref HEAD'.execute().text.trim())
        versionFull = "${versionBase}-${versionBuild}"
        versionSource = versionBase
    }
}

// Storing the version information
project.ext.versionBuild = versionBuild
project.ext.versionBase = versionBase
project.ext.versionFull = versionFull
project.ext.versionCommit = versionCommit
project.ext.versionSource = versionSource

/**
 * Task to display the version information
 */
task displayVersion << {
    println "ontrack.version.full = ${project.version}"
    println "ontrack.version.base = ${project.versionBase}"
    println "ontrack.version.build = ${project.versionBuild}"
    println "ontrack.version.commit = ${project.versionCommit}"
    println "ontrack.version.source = ${project.versionSource}"
}

/**
 * Task to write the information down into a `version.properties` file
 */
task writeVersion << {
    project.file('version.properties').text = """\
ONTRACK_VERSION_FULL = ${project.version}
ONTRACK_VERSION_BASE = ${project.versionBase}
ONTRACK_VERSION_BUILD = ${project.versionBuild}
ONTRACK_VERSION_COMMIT = ${project.versionCommit}
ONTRACK_VERSION_SOURCE = ${project.versionSource}
"""
}