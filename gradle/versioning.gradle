/**
 * Versioning policy.
 *
 * The following information must be collected:
 *      - the full version - will be used for the project's version.
 *      - the base version - version information without the build number
 *      - the build version - the build number
 *      - the commit - the commit for this version
 *
 * The way this information is collected depends on the environment:
 *
 * - if not .git information is available, we cannot determine the version:
 *     - base = projectVersion (from the gradle.properties file)
 *     - build = NA
 *     - <full> = <base>-<build>
 *     - commit = NONE
 *
 * - from a tag (we want to build from a tag)
 *     - the working copy is associated with a tag
 *     - the information is computed from the tag name: <full> = <base>-<build>
 *     - commit = current commit
 *
 * - in a build environment:
 *     - the working copy is not associated with a tag
 *     - the BUILD_NUMBER environment variable is available
 *     - base = projectVersion (from the gradle.properties file)
 *     - build = BUILD_NUMBER
 *     - <full> = <base>-<build>
 *     - commit = current commit
 *
 * - in development mode
 *     - the working copy is not associated with a tag
 *     - the BUILD_NUMBER environment variable is not available
 *     - base = projectVersion (from the gradle.properties file)
 *     - build = DEV
 *     - <full> = <base>-<build>
 *     - commit = current commit
 *
 * - from a tag (we want to build from a tag)
 */

// Information to collect
def versionBase = projectVersion
def versionBuild
def versionFull
def versionCommit

boolean hasGit = project.file('.git').exists()

// No git information
if (!hasGit) {
    versionBuild = 'NA'
    versionFull = "${versionBase}-${versionBuild}"
    versionCommit = 'NONE'
}

// We can use git
else {
    // Gets the current commit
    versionCommit = 'git log -1 --format=%H'.execute().text
    // Gets the tag info
    def tagInfo = 'git describe --exact-match'.execute().text
    // We have a tag info
    if (tagInfo) {
        versionFull = tagInfo
        // Extract base & build from the tag info using a regex
    }
    // We do not have tag info
    else {
        // ... but we have a build number
        if (System.env.BUILD_NUMBER) {
            versionBuild = System.env.BUILD_NUMBER
            versionFull = "${versionBase}-${versionBuild}"
        }
        // ... or not (we are a developer)
        else {
            versionBuild = 'DEV'
            versionFull = "${versionBase}-${versionBuild}"
        }
    }
}

// Storing the version information
project.ext.versionBuild = versionBuild
project.ext.versionBase = versionBase
project.ext.versionFull = versionFull
project.ext.versionCommit = versionCommit
