buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:1.2.0'
    }
}

// Plug-ins

apply plugin: 'base'
apply plugin: 'com.moowork.node'

// Node environment

ext {
    nodeDir = project.file("${project.buildDir}")
}

node {
    version = '4.2.2'
    npmVersion = '2.14.7'
    download = true

    // Set the work directory for unpacking node
    workDir = project.file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = project.file("${project.buildDir}/npm")

    // Set the work directory where node_modules should be located
    nodeModulesDir = project.nodeDir
}

// Environment

ext {
    cacheDir = gradle.getGradleUserHomeDir() as String ?: "${System.getProperty("user.home")}/.cache/gradle"
}

task bowerInstall(type: NodeTask, dependsOn: 'npm_install') {
    script = file('node_modules/bower/bin/bower')
    def bowerArgs = ['install']
    if (project.properties.bowerOptions) {
        bowerArgs.add(project.properties.bowerOptions as String)
    }
    args = bowerArgs
    inputs.files file('bower.json')
    outputs.files file('vendor')
}

// Cleanup

clean.delete << file('build/web')

// Web packaging

task dev(type: NodeTask, dependsOn: bowerInstall) {
    script = file('node_modules/gulp/bin/gulp')
    args = ['dev']
}

task prod(type: NodeTask, dependsOn: bowerInstall) {
    script = file('node_modules/gulp/bin/gulp')
    args = ['default', '--version', versioning.info.display]
    inputs.dir file('src')
    inputs.file file('bower.json')
    inputs.file file('gulpfile.js')
    inputs.file file('package.json')
    ext.outputDir = file('build/web/prod')
    outputs.dir outputDir
}

build.dependsOn prod

task watch(type: NodeTask, dependsOn: dev) {
    script = file('node_modules/gulp/bin/gulp')
    args = ['watch']
}
